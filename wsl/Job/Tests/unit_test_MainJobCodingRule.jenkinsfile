import org.codehaus.groovy.runtime.MethodClosure    /* MethodClosure */

// test_target_jenkinsfile -> テスト対象 jenkinsfile の load 結果
// common -> common.jenkinsfile の load 結果
def test_suite(def test_target_jenkinsfile, def common) {
    List results = []

    results.add(check_comment(test_target_jenkinsfile, common))
    results.add(check_indent(test_target_jenkinsfile, common))
    results.add(check_line_tail(test_target_jenkinsfile, common))
    results.add(check_define_class(test_target_jenkinsfile, common))
    results.add(check_define_global_constant(test_target_jenkinsfile, common))
    // results.add(check_define_global_var(test_target_jenkinsfile, common))

    // 結果確認
    return common.did_success_all(results)
}

def check_comment(def jenkinsfile, def common) {
    boolean result = true

    // OK パターン
    List oks = ["    /* OK */",
                "    /*",
                "    */",
                "/* OK */",
                "/*",
                "*/"
               ]
    if (!check_ok_pattern(jenkinsfile.&is_ok_comment, oks, common)) {
        result = false
    }

    // NG パターン
    List ngs = ["    // ",          // "//" は NG
                "    /*NG */",      // 半角スペースない
                "    /* NG*/",      // 半角スペースない
                "//",
                "/*NG */",
                "/* NG*/"
               ]
    if (!check_ng_pattern(jenkinsfile.&is_ok_comment, ngs, common)) {
        result = false
    }

    // Ignore
    // /****                              : 何らかの定義のヘッダコメントを想定、チェックしない
    // String var_ng3 = ""    // コメント : 行末らへんの // は諦める

    common.print_result(result, "check_comment")
    return result
}

def check_indent(def jenkinsfile, def common) {
    boolean result = true

    // OK パターン
    List oks = ["    int indent = 0",       // 4 の倍数
                "        int indent = 0"    // 4 の倍数
               ]
    if (!check_ok_pattern(jenkinsfile.&is_ok_indent_num, oks, common)) {
        result = false
    }

    // NG パターン
    List ngs = ["     int indent_ng1 = 0",    // 4 の倍数でない
                "   int indent_ng2 = 0"       // 4 の倍数でない
               ]
    if (!check_ng_pattern(jenkinsfile.&is_ok_indent_num, ngs, common)) {
        result = false
    }

    // Ignore
    // "int n = 0" : ローカル変数ならインデント一切ないのは NG だがチェック不可

    common.print_result(result, "check_indent")
    return result
}

def check_line_tail(def jenkinsfile, def common) {
    boolean result = true

    // OK パターン
    List oks = ["    int indent = 0",       // 行末に " " がなければ OK
                "    }"
               ]
    if (!check_ok_pattern(jenkinsfile.&is_ok_line_tail, oks, common)) {
        result = false
    }

    // NG パターン
    List ngs = ["    /* NG */ ",                         // 行末に " " がある
                "    String line_tail_ng = \"\"    ",
                "    }    "
               ]
    if (!check_ng_pattern(jenkinsfile.&is_ok_line_tail, ngs, common)) {
        result = false
    }

    common.print_result(result, "check_line_tail")
    return result
}

def check_define_class(def jenkinsfile, def common) {
    boolean result = true

    // OK パターン
    List oks = ["class ClassDefineOK1 {",
                "class ClassDefineOK2 implements Cloneable {"
               ]
    if (!check_ok_pattern(jenkinsfile.&is_match_class_format, oks, common)) {
        result = false
    }

    // NG パターン
    List ngs = ["class class_define_ng1 {",                        // クラス名は UpperCamelCase
                "class ClassDefineNG2{",                           // 半角スペースない
                "class ClassDefineNG3  {",                         // 半角スペース多すぎ
                "class ClassDefineNG4 extends  Cloneable {",       // 半角スペース多すぎ
                "class ClassDefineNG5 implements Cloneable{"       // 半角スペースない
               ]
    if (!check_ng_pattern(jenkinsfile.&is_match_class_format, ngs, common)) {
        result = false
    }

    common.print_result(result, "check_define_class")
    return result
}

def check_define_global_constant(def jenkinsfile, def common) {
    boolean result = true

    // OK パターン
    List oks = ["GLOBAL_CONSTANT_OK1 =",
                "GLOBAL_CONSTANT_OK2   =   null"
               ]
    if (!check_ok_pattern(jenkinsfile.&is_match_global_constant, oks, common)) {
        result = false
    }

    // NG パターン
    List ngs = ["GLOBAL_CONSTANT_ng1 = null",    // 小文字使用
                "GLOBAL_CONSTANT_NG2= null",     // 半角スペースない
                "GLOBAL_CONSTANT_NG3 =null"      // 半角スペースない
               ]
    if (!check_ng_pattern(jenkinsfile.&is_match_global_constant, ngs, common)) {
        result = false
    }

    common.print_result(result, "check_define_global_constant")
    return result
}

def check_define_global_var(def jenkinsfile, def common) {
    // TODO: 実装
}

def check_ok_pattern(MethodClosure test_func, List oks, def common) {
    // MethodClosure を使うと以下警告が出るが、無視してOK
    // expected to call org.codehaus.groovy.runtime.MethodClosure.call but wound up catching Script4.is_match_class_format; see: https://jenkins.io/redirect/pipeline-cps-method-mismatches/
    List results = oks.collect {
        common.is_true(test_func(it, 1), it)
    }

    return common.did_success_all(results)
}

def check_ng_pattern(MethodClosure test_func, List ngs, def common) {
    List results = ngs.collect {
        common.is_false(test_func(it, 1), it)
    }

    return common.did_success_all(results)
}

return this

import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

g_edited_for_testing_jenkinsfile_path = null    // テストしたい jenkinsfile をテストできるように編集したファイルの保存先
g_test_target_jenkinsfile_edited = null         // g_edited_for_testing_jenkinsfile_path を load した結果
g_unit_test_jenkinsfile = null                  // unit_test_***.jenkinsfile の load 結果
g_common_jenkinsfile = null                     // common.jenkinsfile の load 結果
g_coding_rule_jenkinsfile = null                // コーディングルールのチェックロジックの入った jenkinsfile の load 結果

pipeline {
    agent any

    stages {
        stage('Init') {
            steps {
                timestamps {
                    script {
                        init(UNIT_TEST_JENKINSFILE_PATH,
                             TEST_TARGET_JOB_JENKINSFILE_PATH,
                             COMMON_JENKINSFILE_PATH,
                             WORKSPACE)
                    }
                }
            }
        }

        stage('Create test script') {
            steps {
                timestamps {
                    script {
                        create_test_script(TEST_TARGET_JOB_JENKINSFILE_PATH,
                                           g_edited_for_testing_jenkinsfile_path)
                    }
                }
            }
        }

        stage('Check coding rule') {
            steps {
                // コーディング規約に違反していてもテストは可能なのでジョブを止めない
                catchError(stageResult: 'FAILURE') {
                    timestamps {
                        script {
                            is_applied_coding_rule(TEST_TARGET_JOB_JENKINSFILE_PATH,
                                                   g_coding_rule_jenkinsfile,
                                                   g_common_jenkinsfile)
                        }
                    }
                }
            }
        }

        stage('Load test script') {
            steps {
                timestamps {
                    script {
                        g_test_target_jenkinsfile_edited = load_test_script(g_edited_for_testing_jenkinsfile_path,
                                                                            g_common_jenkinsfile)
                    }
                }
            }
        }

        stage('Test execute') {
            steps {
                timestamps {
                    script {
                        execute_test_suite(g_test_target_jenkinsfile_edited,
                                           g_unit_test_jenkinsfile,
                                           g_common_jenkinsfile)
                    }
                }
            }
        }
    }

    post {
        // 失敗したときは環境確認のためにワークスペースをそのまま残す
        success {
            deleteDir()
        }
    }
}

// 各種 jenkinsfile を load
// テストしたい jenkinsfile をテストできるように編集したファイルの保存先パスを作成
def init(String unit_test_jenkinsfile_path,
         String test_target_job_jenkinsfile_path,
         String common_jenkinsfile_path,
         String workspace_path) {
    println("workspace: ${workspace_path}")
    ls(workspace_path)

    String common_dir_path = new File(common_jenkinsfile_path).getParent()
    String coding_rule_path = "${common_dir_path}/check_coding_rule.jenkinsfile"

    g_common_jenkinsfile = load(common_jenkinsfile_path)
    g_coding_rule_jenkinsfile = g_common_jenkinsfile.load_script(coding_rule_path)
    g_unit_test_jenkinsfile = g_common_jenkinsfile.load_script(unit_test_jenkinsfile_path)
    g_edited_for_testing_jenkinsfile_path = create_edited_for_testing_jenkinsfile_path(test_target_job_jenkinsfile_path,
                                                                                       workspace_path)
}

def ls(String dir_path) {
    try {
        sh("ls -l ${dir_path}")
    } catch (Exception e) {
        powershell("dir ${dir_path}")
    }
}

def create_edited_for_testing_jenkinsfile_path(String test_target_job_jenkinsfile_path,
                                               String workspace_path) {
    String jenkinsfile_name = new File(test_target_job_jenkinsfile_path).getName()
    String unique_id = get_unique_id()
    return "${workspace_path}/${jenkinsfile_name}_${unique_id}.groovy"
}

def get_unique_id() {
    def date = LocalDateTime.now()
    def now = date.format(DateTimeFormatter.ofPattern('yyyyMMdd_HHmmss'))
    return "${now}_${BUILD_ID}"
}

// テストしたい jenkinsfile をテストできるように編集し、保存
def create_test_script(String test_target_job_jenkinsfile_path,
                       String edited_for_testing_jenkinsfile_path) {
    if (!(new File(test_target_job_jenkinsfile_path).exists())) {
        error("file not exist: ${test_target_job_jenkinsfile_path}")
    }

    g_common_jenkinsfile.create_script_edited_for_testing(test_target_job_jenkinsfile_path,
                                                          edited_for_testing_jenkinsfile_path)
}

// コーディングルールの遵守をチェック
// (チェックには限界があるのでこれが通れば全て OK という訳ではない)
def is_applied_coding_rule(String test_target_job_jenkinsfile_path,
                           def coding_rule_jenkinsfile,
                           def common_jenkinsfile) {
    if (coding_rule_jenkinsfile.is_applied(test_target_job_jenkinsfile_path, common_jenkinsfile)) {
        println("coding rule ... OK.")
    } else {
        error("coding rule ... NG.")
    }
}

// テストしたい jenkinsfile をテストできるように編集したファイルを load
def load_test_script(String edited_for_testing_jenkinsfile_path, def common_jenkinsfile) {
    return g_common_jenkinsfile.load_script(edited_for_testing_jenkinsfile_path)
}

// テストしたい jenkinsfile にテストを実施
def execute_test_suite(def test_target_jenkinsfile,
                       def test_suite_jenkinsfile,
                       def common_jenkinsfile) {
    boolean result = test_suite_jenkinsfile.test_suite(test_target_jenkinsfile, common_jenkinsfile)
    if (result) {
        println("test success.")
    } else {
        error("test failed")
    }

    return result
}
